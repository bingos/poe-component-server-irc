use strict;
use warnings;
use Test::More 'no_plan';
use POE qw[Filter::Stackable Filter::Line Filter::IRCD];
use POE::Component::Server::IRC;
use Test::POE::Client::TCP;
use IRC::Utils qw[BOLD YELLOW NORMAL];

my @responses = (
':listen.server.irc 015 bobbins :listen.server.irc ---------------------------------------------- | Users:     1 (33.33%)',
':listen.server.irc 015 bobbins :|-harpo.server.irc --------------------------------------------- | Users:     1 (33.33%)',
':listen.server.irc 015 bobbins :`-groucho.server.irc ------------------------------------------- | Users:     1 (33.33%)',
':listen.server.irc 015 bobbins :  `-fake.server.irc -------------------------------------------- | Users:     0 (0.00%)',
':listen.server.irc 015 bobbins :    `-harry.server.irc ----------------------------------------- | Users:     0 (0.00%)',
':listen.server.irc 015 bobbins :      `-larry.server.irc --------------------------------------- | Users:     0 (0.00%)',
':listen.server.irc 015 bobbins :        `-barry.server.irc ------------------------------------- | Users:     0 (0.00%)',
':listen.server.irc 015 bobbins :          `-garry.server.irc ----------------------------------- | Users:     0 (0.00%)',
':listen.server.irc 015 bobbins :            `-marry.server.irc --------------------------------- | Users:     0 (0.00%)',
':listen.server.irc 015 bobbins :              `-carry.server.irc ------------------------------- | Users:     0 (0.00%)',
':listen.server.irc 017 bobbins :End of /MAP',
);

my %servers = (
 'listen.server.irc'   => '1FU',
 'groucho.server.irc'  => '7UP',
 'harpo.server.irc'    => '9T9',
 'fake.server.irc'     => '4AK',
 'harry.server.irc'    => '1HP',
 'larry.server.irc'    => '2HP',
 'barry.server.irc'    => '3HP',
 'garry.server.irc'    => '4HP',
 'marry.server.irc'    => '5HP',
 'carry.server.irc'    => '6HP',
);

my $ts = time();

my $uidts;

my $pocosi = POE::Component::Server::IRC->spawn(
    auth         => 0,
    antiflood    => 0,
    plugin_debug => 1,
    config => { servername => 'listen.server.irc', sid => '1FU', anti_spam_exit_message_time => 0 },
);

POE::Session->create(
    package_states => [
        'main' => [qw(
            _start
            _shutdown
            _launch_client
            ircd_listener_add
            ircd_daemon_eob
            groucho_connected
            groucho_input
            groucho_disconnected
            harpo_connected
            harpo_input
            harpo_disconnected
            client_connected
            client_input
            client_disconnected
        )],
        'main' => {
            groucho_registered => 'testc_registered',
            harpo_registered   => 'testc_registered',
            client_registered  => 'testc_registered',
        },
    ],
    heap => {
      ircd  => $pocosi,
      eob   => 1,
      topic => 0,
    },
);

$poe_kernel->run();

sub _start {
    my ($kernel, $heap) = @_[KERNEL, HEAP];
    $heap->{ircd}->yield('register', 'all');
    $heap->{ircd}->add_listener();
    $kernel->delay('_shutdown', 60, 'timeout');
}

sub _shutdown {
    my $heap = $_[HEAP];
    if ( $_[ARG0] && $_[ARG0] eq 'timeout' ) {
      fail('We timed out');
    }
    exit;
    return;
}

sub ircd_listener_add {
    my ($heap, $port) = @_[HEAP, ARG0];
    pass("Started a listener on $port");
    $heap->{port} = $port;
    $heap->{ircd}->add_peer(
        name  => 'groucho.server.irc',
        pass  => 'foo',
        rpass => 'foo',
        type  => 'c',
        zip   => 1,
    );
    $heap->{ircd}->add_peer(
        name  => 'harpo.server.irc',
        pass  => 'foo',
        rpass => 'foo',
        type  => 'c',
        zip   => 1,
    );
    foreach my $tag ( qw[groucho harpo] ) {
      my $filter = POE::Filter::Stackable->new();
      $filter->push( POE::Filter::Line->new( InputRegexp => '\015?\012', OutputLiteral => "\015\012" ),
                POE::Filter::IRCD->new( debug => 0 ), );
      push @{ $heap->{testc} }, Test::POE::Client::TCP->spawn( alias => $tag, filter => $filter, address => '127.0.0.1', port => $port, prefix => $tag );
    }
    return;
}

sub _launch_client {
  my ($kernel,$heap) = @_[KERNEL,HEAP];
  my $filter = POE::Filter::Stackable->new();
  $filter->push( POE::Filter::Line->new( InputRegexp => '\015?\012', OutputLiteral => "\015\012" ),
             POE::Filter::IRCD->new( debug => 0 ), );
  my $tag = 'client';
  $heap->{client} = Test::POE::Client::TCP->spawn( alias => $tag, filter => $filter, address => '127.0.0.1', port => $heap->{port}, prefix => $tag );
  return;
}

sub testc_registered {
  my ($kernel,$sender) = @_[KERNEL,SENDER];
  pass($_[STATE]);
  $kernel->post( $sender, 'connect' );
  return;
}

sub client_connected {
  my ($kernel,$heap,$sender) = @_[KERNEL,HEAP,SENDER];
  pass($_[STATE]);
  $kernel->post( $sender, 'send_to_server', { command => 'NICK', params => [ 'bobbins' ], colonify => 0 } );
  $kernel->post( $sender, 'send_to_server', { command => 'USER', params => [ 'bobbins', '*', '*', 'bobbins along' ], colonify => 1 } );
  return;
}

sub groucho_connected {
  my ($kernel,$heap,$sender) = @_[KERNEL,HEAP,SENDER];
  pass($_[STATE]);
  $kernel->post( $sender, 'send_to_server', { command => 'PASS', params => [ 'foo', 'TS', '6', '7UP' ], } );
  $kernel->post( $sender, 'send_to_server', { command => 'CAPAB', params => [ 'KNOCK UNDLN DLN TBURST GLN ENCAP UNKLN KLN CHW IE EX HOPS SVS CLUSTER EOB QS' ], colonify => 1 } );
  $kernel->post( $sender, 'send_to_server', { command => 'SERVER', params => [ 'groucho.server.irc', '1', 'Open the door and come in!!!!!!' ], colonify => 1 } );
  $kernel->post( $sender, 'send_to_server', { command => 'SVINFO', params => [ '6', '6', '0', time() ], colonify => 1 } );
  $uidts = time() - 20;
  $kernel->post( $sender, 'send_to_server', { prefix => '7UP', command => 'SID', params => [ 'fake.server.irc', 2, '4AK', 'This is a fake server' ] } );
  $kernel->post( $sender, 'send_to_server', { prefix => '4AK', command => 'SID', params => [ 'harry.server.irc', 3, '1HP', 'This is a fake server' ] } );
  $kernel->post( $sender, 'send_to_server', { prefix => '1HP', command => 'SID', params => [ 'larry.server.irc', 4, '2HP', 'This is a fake server' ] } );
  $kernel->post( $sender, 'send_to_server', { prefix => '2HP', command => 'SID', params => [ 'barry.server.irc', 5, '3HP', 'This is a fake server' ] } );
  $kernel->post( $sender, 'send_to_server', { prefix => '3HP', command => 'SID', params => [ 'garry.server.irc', 6, '4HP', 'This is a fake server' ] } );
  $kernel->post( $sender, 'send_to_server', { prefix => '4HP', command => 'SID', params => [ 'marry.server.irc', 7, '5HP', 'This is a fake server' ] } );
  $kernel->post( $sender, 'send_to_server', { prefix => '5HP', command => 'SID', params => [ 'carry.server.irc', 8, '6HP', 'This is a fake server' ] } );
  $kernel->post( $sender, 'send_to_server', { prefix => '7UP', command => 'UID', params => [ 'groucho', '1', $uidts, '+aiow', 'groucho', 'groucho.marx', '0', '7UPAAAAAA', '0', 'Groucho Marx' ], colonify => 1 } );
  $kernel->post( $sender, 'send_to_server', { command => 'EOB', prefix => $_ } ) for qw[7UP 4AK 1HP 2HP 3HP 4HP 5HP 6HP];
  $kernel->post( $sender, 'send_to_server', { command => 'PING', params => [ '7UP' ], colonify => 1 } );
  return;
}

sub harpo_connected {
  my ($kernel,$heap,$sender) = @_[KERNEL,HEAP,SENDER];
  pass($_[STATE]);
  $kernel->post( $sender, 'send_to_server', { command => 'PASS', params => [ 'foo', 'TS', '6', '9T9' ], } );
  $kernel->post( $sender, 'send_to_server', { command => 'CAPAB', params => [ 'KNOCK UNDLN DLN TBURST GLN ENCAP UNKLN KLN CHW IE EX HOPS SVS CLUSTER EOB QS' ], colonify => 1 } );
  $kernel->post( $sender, 'send_to_server', { command => 'SERVER', params => [ 'harpo.server.irc', '1', 'Open the door and come in!!!!!!' ], colonify => 1 } );
  $kernel->post( $sender, 'send_to_server', { command => 'SVINFO', params => [ '6', '6', '0', time() ], colonify => 1 } );
  $uidts = time() - 20;
  $kernel->post( $sender, 'send_to_server', { prefix => '9T9', command => 'UID', params => [ 'harpo', '1', $uidts, '+aiow', 'harpo', 'harpo.marx', '0', '9T9AAAAAA', '0', 'Harpo Marx' ], colonify => 1 } );
  $kernel->post( $sender, 'send_to_server', { command => 'EOB', prefix => $_ } ) for qw[9T9];
  $kernel->post( $sender, 'send_to_server', { command => 'PING', params => [ '9T9' ], colonify => 1 } );
  return;
}


sub client_input {
  my ($heap,$sender,$in) = @_[HEAP,SENDER,ARG0];
  #diag($in->{raw_line}, "\n");
  my $prefix = $in->{prefix};
  my $cmd    = $in->{command};
  my $params = $in->{params};
  if ( $cmd eq 'MODE' && $prefix =~ m'^bobbins' && $params->[1] eq '+i' ) {
    pass($cmd);
    $poe_kernel->post( $sender, 'send_to_server', { command => 'MAP' } );
  }
  if ( $cmd =~ m!^(01[567])$! ) {
    my $resp = shift @responses;
    is($in->{raw_line},$resp,$resp);
    if ( !scalar @responses ) {
      $poe_kernel->state( 'client_input', sub {
          my ($heap,$sender,$in) = @_[HEAP,SENDER,ARG0];
          pass($in->{command});
          $poe_kernel->post( $sender, 'shutdown' );
          $poe_kernel->post( 'harpo', 'terminate' );
          return;
      } );
      $poe_kernel->post($sender, 'send_to_server', { command => 'QUIT', params => [ 'Connection reset by fear' ] } );
    }
    return;
  }
  return;
}

sub groucho_input {
  my ($heap,$sender,$in) = @_[HEAP,SENDER,ARG0];
  #diag($in->{raw_line}, "\n");
  my $prefix = $in->{prefix};
  my $cmd    = $in->{command};
  my $params = $in->{params};
  if ( $cmd eq 'QUIT' ) {
    pass($cmd);
    is( $prefix, '1FUAAAAAA', 'Correct prefix: 1FUAAAAAAA' );
    is( $params->[0], q{Quit: "Connection reset by fear"}, 'Correct QUIT message' );
    return;
  }
  if ( $cmd eq 'SQUIT' ) {
    pass($cmd);
    is( $params->[0], '9T9', 'Correct SID: 9T9' );
    like( $params->[1], qr/^(Remote host closed the connection|Connection reset by peer)$/, 'Remote host closed the connection' );
    $poe_kernel->post( $sender, 'terminate' );
    return;
  }
  if ( $cmd eq 'AWAY' ) {
    pass($cmd);
    if ( $params->[0] ) {
      is( $params->[0], 'Do not bother me!', 'Do not bother me!' );
      return;
    }
    pass('AWAY is unset');
  }
  if ( $cmd eq 'LINKS' ) {
    pass($cmd);
    is( $prefix, '1FUAAAAAA', 'LINKS cmd from 1FUAAAAAA' );
    is( $params->[0], '4AK', 'Target is fake.server.irc' );
    is( $params->[1], '*', 'The mask is for everything' );
    my $sid = '4AK';
    my @replies = (
      [ '364', 'harpo.server.irc', 'listen.server.irc', '3 Open the door and come in!!!!!!' ],
      [ '364', 'listen.server.irc', 'groucho.server.irc', '2 Poco? POCO? POCO!' ],
      [ '364', 'groucho.server.irc', 'fake.server.irc', '1 Open the door and come in!!!!!!' ],
      [ '364', 'fake.server.irc', 'fake.server.irc', '0 This is a fake server' ],
      [ '365', $params->[1], 'End of /LINKS list.' ],
    );
    foreach my $reply ( @replies ) {
        my $numeric = shift @$reply;
        unshift @$reply, $prefix;
        $poe_kernel->post( $sender, 'send_to_server', { prefix => $sid, command => $numeric, params => $reply, colonify => 1 } );
    }
    return;
  }
  return;
}

sub harpo_input {
  my ($heap,$in) = @_[HEAP,ARG0];
  #diag($in->{raw_line}, "\n");
  my $prefix = $in->{prefix};
  my $cmd    = $in->{command};
  my $params = $in->{params};
  if ( $cmd eq 'QUIT' ) {
    pass($cmd);
    is( $prefix, '1FUAAAAAA', 'Correct prefix: 1FUAAAAAAA' );
    is( $params->[0], q{Quit: "Connection reset by fear"}, 'Correct QUIT message' );
    return;
  }
  if ( $cmd eq 'AWAY' ) {
    pass($cmd);
    if ( $params->[0] ) {
      is( $params->[0], 'Do not bother me!', 'Do not bother me!' );
      return;
    }
    pass('AWAY is unset');
  }
  return;
}

sub client_disconnected {
  my ($heap,$state,$sender) = @_[HEAP,STATE,SENDER];
  pass($state);
  return;
}

sub groucho_disconnected {
  my ($heap,$state,$sender) = @_[HEAP,STATE,SENDER];
  pass($state);
  $poe_kernel->call( $sender, 'shutdown' );
  $heap->{ircd}->yield('shutdown');
  $poe_kernel->delay('_shutdown');
  return;
}

sub harpo_disconnected {
  my ($heap,$state,$sender) = @_[HEAP,STATE,SENDER];
  pass($state);
  $poe_kernel->call( $sender, 'shutdown' );
  #$poe_kernel->post( 'groucho', 'terminate' );
  return;
}

sub ircd_daemon_eob {
  my ($kernel,$heap,$sender,@args) = @_[KERNEL,HEAP,SENDER,ARG0..$#_];
  $heap->{eob}++;
  pass($_[STATE]);
  if ( defined $servers{ $args[0] } ) {
    pass('Correct server name in EOB: ' . $args[0]);
    is( $args[1], $servers{ $args[0] }, 'Correct server ID in EOB: ' . $args[1] );
  }
  else {
    fail('No such server expected');
  }
  if ( $heap->{eob} >= scalar keys %servers ) {
      $poe_kernel->yield('_launch_client');
  }
  return;
}
